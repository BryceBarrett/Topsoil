
import org.apache.tools.ant.filters.ReplaceTokens
import org.eclipse.jgit.api.Git
import org.eclipse.jgit.internal.storage.file.FileRepository

def mainClass = 'org.cirdles.topsoil.app.Topsoil'

def getVersionName = { ->
    def repo = new FileRepository("$rootDir/.git")
    def git = new Git(repo)

    git.describe().call()
}

processResources {
    from('src/main/resources') {
        include '**/*.properties'
        filter(ReplaceTokens, tokens: [version: getVersionName()])
    }
}

def configJavaExec = { JavaExec task ->
    task.main = mainClass
    task.classpath = sourceSets.main.runtimeClasspath
    task.standardInput = System.in
}

task run(dependsOn: jar, type: JavaExec) {
    configJavaExec(it)
}

task debug(dependsOn: jar, type: JavaExec) {
    configJavaExec(it)
    debug = true
}

task jarWithDependencies(type: Jar) {
    manifest.attributes(
            'Implementation-Title': 'Topsoil',
            'Implementation-Version': getVersionName(),
            'Built-JDK': System.getProperty('java.version'),
            'Main-Class': mainClass
    )
    baseName = 'topsoil-' + getVersionName().split('-')[0]
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } 
    with jar
}

dependencies {
    compile project(':topsoilCore')

    compileOnly 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.controlsfx:controlsfx:8.40.14'
    compile 'com.google.inject:guice:4.0'

    compile 'org.springframework:spring-jdbc:4.2.0.RELEASE'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "org.testfx:testfx-core:4.0.15-alpha"
    testCompile "org.testfx:openjfx-monocle:8u76-b04"
    testCompile "org.testfx:testfx-junit:4.0.15-alpha"
}

buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'kotlin'
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'com.github.hudsonb:kubed:-SNAPSHOT'

}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
